// module和lib控制
if (isModule) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
android {
    compileSdkVersion versions.compileSdkVersion
    buildToolsVersion versions.buildToolsVersion

    defaultConfig {
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode versions.versionCode
        versionName "1.0"

        // 模块和lib控制
        if (isModule) {
            applicationId appIds.device
        }

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }
    //配置keystore签名
    signingConfigs {
        debugConfig {
            storeFile file(keystore.storeFile)
            storePassword keystore.storePassword
            keyAlias keystore.keyAlias
            keyPassword keystore.keyPassword
        }

        releaseConfig {
            storeFile file(keystore.storeFile)
            storePassword keystore.storePassword
            keyAlias keystore.keyAlias
            keyPassword keystore.keyPassword
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            java.srcDirs "src/main/java"
            java.srcDirs += 'src/main/kotlin'
            jniLibs.srcDirs = ['libs']
            res.srcDirs "src/main/res"

            if (isModule) {// module app形式
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else { // lib形式
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"
    implementation "com.android.support:appcompat-v7:${versions.support}"

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation "junit:junit:${versions.junit}"
    androidTestImplementation "com.android.support.test:runner:${versions.runner}"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${versions.espresso}"



    // other aar/jareventbus
    // recyclerViewHelper
    implementation "com.github.CymChad:BaseRecyclerViewAdapterHelper:${versions.recyclerViewHelper}"
    implementation "com.android.support:recyclerview-v7:${versions.support}"

    api project(path: ':libCommon')
    api project(path: ':resCommon')

    // Glide 图片库
    implementation "com.github.bumptech.glide:glide:${versions.glide}"
    implementation "com.github.bumptech.glide:annotations:${versions.glide}"
    annotationProcessor "com.github.bumptech.glide:compiler:${versions.glide}"
}
